import com.vanniktech.maven.publish.AndroidSingleVariantLibrary

plugins {
    id "com.android.library"
    id "com.vanniktech.maven.publish"
    id "com.gradleup.nmcp"
}

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.arthenica.ffmpegkit'
    }
    compileSdk 35
    ndkVersion "22.1.7171670"

    defaultConfig {
        minSdk 24
        targetSdk 35
        versionCode 240600
        versionName "6.0"
        project.archivesBaseName = "ffmpeg-kit"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["../libs"]
        }
    }

    publishing {
        singleVariant("release")
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            systemProperty 'enable.ffmpeg.kit.test.mode', 'true'
        }
    }
}

task javadoc(type: Javadoc) {
    title = 'FFmpegKit'
    destinationDir = file("${projectDir}/../../docs/android/javadoc")
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    source = android.sourceSets.main.java.srcDirs
    configurations.implementation.setCanBeResolved(true)
    classpath += configurations.implementation
}

mavenPublishing {
  // the first parameter represennts which variant is published
  // the second whether to publish a sources jar
  // the third whether to publish a javadoc jar
  configure(new AndroidSingleVariantLibrary("release", true, true))
}

dependencies {
    api 'com.arthenica:smart-exception-java:0.2.1'
    testImplementation "androidx.test.ext:junit:1.1.5"
    testImplementation 'org.json:json:20230618'
}

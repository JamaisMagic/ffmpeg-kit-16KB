import com.vanniktech.maven.publish.AndroidSingleVariantLibrary

plugins {
    id "com.android.library"
    id "com.vanniktech.maven.publish"
}

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.arthenica.ffmpegkit'
    }
    compileSdk 35
    ndkVersion "22.1.7171670"

    defaultConfig {
        minSdk 16
        targetSdk 35
        versionCode 160600
        versionName "6.0.LTS"
        project.archivesBaseName = "ffmpeg-kit"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["../libs"]
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            systemProperty 'enable.ffmpeg.kit.test.mode', 'true'
        }
    }


    publishing {
        singleVariant('release') {
            withJavadocJar()
            withSourcesJar()
        }
    }
}

task javadoc(type: Javadoc) {
    title = 'FFmpegKit'
    destinationDir = file("${projectDir}/../../docs/android/javadoc")
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    source = android.sourceSets.main.java.srcDirs
    configurations.implementation.setCanBeResolved(true)
    classpath += configurations.implementation
}

dependencies {
    api 'com.arthenica:smart-exception-java:0.2.1'
    testImplementation "androidx.test.ext:junit:1.1.5"
    testImplementation 'org.json:json:20230618'
}

mavenPublishing {
  // the first parameter represennts which variant is published
  // the second whether to publish a sources jar
  // the third whether to publish a javadoc jar
//   configure(new AndroidSingleVariantLibrary("release", true, true))
    publishToMavenCentral()
    signAllPublications()

    coordinates("io.github.jamaismagic", "ffmpeg-kit-16kb", "6.0.0")

    pom {
        name = "FFMPEG Android 16KB"
        description = "FFmpeg Kit for Android (Maven Package for https://github.com/JamaisMagic/ffmpeg-kit-16KB)."
        inceptionYear = "2025"
        url = "https://github.com/JamaisMagic/ffmpeg-kit-16KB"
        licenses {
            license {
                name = "GNU LESSER GENERAL PUBLIC LICENSE Version 3"
                url = "https://www.gnu.org/licenses/lgpl-3.0.txt"
                distribution = "https://www.gnu.org/licenses/lgpl-3.0.txt"
            }
        }
        developers {
            developer {
                id = "JamaisMagic"
                name = "JamaisNg"
                url = "https://github.com/JamaisMagic"
            }
        }
        scm {
            url = "https://github.com/JamaisMagic/ffmpeg-kit-16KB"
            connection = "scm:git@github.com:JamaisMagic/ffmpeg-kit-16KB.git"
            developerConnection = "=scm:git@github.com:JamaisMagic/ffmpeg-kit-16KB.git"
        }
    }
}
